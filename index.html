<!DOCTYPE html>
<html>

<head>
    <meta charset="utf-8">
    <title>Plato's Cave</title>
    <LINK REL=StyleSheet HREF="css/styles.css" TYPE="text/css" MEDIA=screen>
</head>

<body>
    



    <script src="lib/three.js"></script>
    <script src="//connect.soundcloud.com/sdk.js"></script>

    <script src="lib/ParticleEngine/ShaderParticles.min.js"></script>

    
    <!-- pass through vertex shader -->
    <script id="vertexShader" type="x-shader/x-vertex">

      void main() {

        gl_Position = vec4( position, 1.0 );

      }

    </script>

    <!-- pass through fragment shader -->
    <script id="fragmentShader" type="x-shader/x-fragment">

      uniform vec2 resolution;
      uniform float time;
      uniform sampler2D texture;

      void main() {

        vec2 uv = gl_FragCoord.xy / resolution.xy;

        vec3 color = texture2D( texture, uv ).xyz;

        gl_FragColor=vec4(color, 1.0);

      }

    </script>
    <!-- end pass through shaders -->

    <!-- shader for bird's position -->
    <script id="fragmentShaderPosition" type="x-shader/x-fragment">

      uniform vec2 resolution;
      uniform float time;
      uniform float delta;
      uniform sampler2D textureVelocity;
      uniform sampler2D texturePosition;

      void main() {

        vec2 uv = gl_FragCoord.xy / resolution.xy;
        vec4 tmpPos = texture2D( texturePosition, uv );
        vec3 position = tmpPos.xyz;
        vec3 velocity = texture2D( textureVelocity, uv ).xyz;

        float phase = tmpPos.w;

        phase = mod( ( phase + delta +
          length( velocity.xz ) * delta * 3. +
          max( velocity.y, 0.0 ) * delta * 6. ), 62.83 );

        gl_FragColor = vec4( position + velocity * delta * 15. , phase );

      }

    </script>

    <!-- shader for bird's velocity -->
    <script id="fragmentShaderVelocity" type="x-shader/x-fragment">

      uniform vec2 resolution;
      uniform float time;
      uniform float testing;
      uniform float delta; // about 0.016
      uniform float seperationDistance; // 20
      uniform float alignmentDistance; // 40
      uniform float cohesionDistance; // 
      uniform float freedomFactor;


      uniform vec3 predator;
      uniform vec3 prey;


      uniform sampler2D textureVelocity;
      uniform sampler2D texturePosition;

      const float width = WIDTH;
      const float height = WIDTH;

      const float PI = 3.141592653589793;
      const float PI_2 = PI * 2.0;
      // const float VISION = PI * 0.55;

      float zoneRadius = 40.0;
      float zoneRadiusSquared = zoneRadius * zoneRadius;

      float separationThresh = 0.45;
      float alignmentThresh = 0.65;

      const float UPPER_BOUNDS = 400.0;
      const float LOWER_BOUNDS = -UPPER_BOUNDS;

      const float SPEED_LIMIT = 9.0;

      float rand(vec2 co){
          return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
      }

      void main() {

        zoneRadius = seperationDistance + alignmentDistance + cohesionDistance;
        separationThresh = seperationDistance / zoneRadius;
        alignmentThresh = ( seperationDistance + alignmentDistance ) / zoneRadius;
        zoneRadiusSquared = zoneRadius * zoneRadius;


        vec2 uv = gl_FragCoord.xy / resolution.xy;
        vec3 birdPosition, birdVelocity;

        vec3 selfPosition = texture2D( texturePosition, uv ).xyz;
        vec3 selfVelocity = texture2D( textureVelocity, uv ).xyz;

        float dist;
        vec3 dir; // direction
        float distSquared;

        float seperationSquared = seperationDistance * seperationDistance;
        float cohesionSquared = cohesionDistance * cohesionDistance;

        float f;
        float percent;

        vec3 velocity = selfVelocity;

        float limit = SPEED_LIMIT;

        dir = predator * UPPER_BOUNDS - selfPosition;
        dir.z = 0.;
        // dir.z *= 0.6;
        dist = length( dir );
        distSquared = dist * dist;

        float preyRadius = 150.0;
        float preyRadiusSq = preyRadius * preyRadius;

        
        // move birds away from predator
        if (dist < preyRadius) {

          f = ( distSquared / preyRadiusSq - 1.0 ) * delta * 100.;
          velocity += normalize( dir ) * f;
          limit += 5.0;
        }


        // if (testing == 0.0) {}
        // if ( rand( uv + time ) < freedomFactor ) {}


        // Attract flocks to the center
        vec3 central = prey;
        dir = selfPosition - central;
        dist = length( dir );
        dir.y *= 2.5;
        velocity -= normalize( dir ) * delta * 5.;

        for (float y=0.0;y<height;y++) {
          for (float x=0.0;x<width;x++) {

            if (
              x == gl_FragCoord.x && y == gl_FragCoord.y) continue;

            birdPosition = texture2D( texturePosition,
              vec2( x / resolution.x,  y / resolution.y ) ).xyz;

            dir = birdPosition - selfPosition;
            dist = length(dir);
            distSquared = dist * dist;

            if ( dist > 0.0 && distSquared < zoneRadiusSquared ) {

              percent = distSquared / zoneRadiusSquared;

              if ( percent < separationThresh ) { // low

                // Separation - Move apart for comfort
                f = (separationThresh / percent - 1.0) * delta;
                velocity -= normalize(dir) * f;

              } else if ( percent < alignmentThresh ) { // high

                // Alignment - fly the same direction
                float threshDelta = alignmentThresh - separationThresh;
                float adjustedPercent = ( percent - separationThresh ) / threshDelta;

                birdVelocity = texture2D( textureVelocity, vec2(x/resolution.x, y/resolution.y) ).xyz;
                
                f = ( 0.5 - cos( adjustedPercent * PI_2 ) * 0.5 + 0.5 ) * delta;
                velocity += normalize(birdVelocity) * f;

              } else {

                // Attraction / Cohesion - move closer
                float threshDelta = 1.0 - alignmentThresh;
                float adjustedPercent = ( percent - alignmentThresh ) / threshDelta;

                 f = ( 0.5 - ( cos( adjustedPercent * PI_2 ) * -0.5 + 0.5 ) ) * delta;

                 velocity += normalize(dir) * f;

              }

            }

          }

        }

        

        // this make tends to fly around than down or up
        // if (velocity.y > 0.) velocity.y *= (1. - 0.2 * delta);

        // Speed Limits
        if ( length( velocity ) > limit ) {
          velocity = normalize( velocity ) * limit;
        }

        gl_FragColor = vec4( velocity, 1.0 );

      }

    </script>

    <script type="x-shader/x-vertex" id="birdVS">

      attribute vec2 reference;
      attribute float birdVertex;

      attribute vec3 birdColor;
      
      uniform sampler2D texturePosition;
      uniform sampler2D textureVelocity;

      varying vec3 vNormal;
      varying vec2 vUv;
      varying vec4 vColor;
      varying float z;

      uniform float time;

      void main() {

        vNormal = normal;

        vec4 tmpPos = texture2D( texturePosition, reference );
        vec3 pos = tmpPos.xyz;
        vec3 velocity = normalize(texture2D( textureVelocity, reference ).xyz);

        vec3 newPosition = position;

        if ( birdVertex == 4.0 || birdVertex == 7.0 ) {
          // flap wings
          newPosition.y = sin( tmpPos.w ) * 5.;
        }

        newPosition = mat3( modelMatrix ) * newPosition;


        velocity.z *= -1.;
        float xz = length( velocity.xz );
        float xyz = 1.;
        float x = sqrt( 1. - velocity.y * velocity.y );
        
        float cosry = velocity.x / xz;
        float sinry = velocity.z / xz;

        float cosrz = x / xyz;
        float sinrz = velocity.y / xyz;

        mat3 maty =  mat3(
          cosry, 0, -sinry,
          0    , 1, 0     ,
          sinry, 0, cosry

        );

        mat3 matz =  mat3(
          cosrz , sinrz, 0,
          -sinrz, cosrz, 0,
          0     , 0    , 1
        );

        newPosition =  maty * matz * newPosition;
        newPosition += pos;

        z = newPosition.z;
        
        vColor = vec4( birdColor, 1.0 );
        gl_Position = projectionMatrix *  viewMatrix  * vec4( newPosition, 1.0 );
      }

    </script>

    <!-- bird geometry shader -->
    <script type="x-shader/x-fragment" id="birdFS">

      varying vec3 vNormal;
      varying vec2 vUv;
      varying vec4 vColor;
      varying float z;

      uniform vec3 color;

      void main() {
        // Fake colors for now
        float z2 = 0.2 +  vColor.x;
        gl_FragColor = vec4( z2, z2, z2, 1. );

        // vec3 light = vec3( 0.0, 1.0, 1.0 );
        // light = normalize( light );
        // float dProd = dot( vNormal, light ) ; //* 0.5 + 0.5;
        // vec4 tcolor = vColor;
        // vec4 gray = vec4( vec3( tcolor.r * 0.3 + tcolor.g * 0.59 + tcolor.b * 0.11 ), 1.0 );

        // gl_FragColor =  gray * dProd;
        // gl_FragColor =  vec4( dProd  * tcolor );

      }

    </script>
<!--     // <script src="lib/ParticleEngine/Pool.js"></script>
    // <script src="lib/ParticleEngine/ShaderParticleEmitter.js"></script>
    // <script src="lib/ParticleEngine/ShaderParticleGroup.js"></script> -->


    <script src="lib/stats.js"></script>
    <script src="build/js/main.js"></script>
    <script src="lib/SimulatorRenderer.js"></script>

    <script src="lib/FlyControls.js"></script>
    <script src ="lib/FastBlur.js"></script>

    <script src="lib/terrain-generator/mersenne-twister.js"></script>
    <script src="lib/terrain-generator/blur.js"></script>
    <script src="lib/terrain-generator/destructure.js"></script>
    <script src="lib/terrain-generator/mountains.js"></script>
    <script src="lib/terrain-generator/perlinnoise.js"></script>
    <script src="lib/terrain-generator/terraingen.js"></script>
    <script src="lib/terrain-generator/circle.js"></script>

    <script src="lib/water-material.js"></script>
    <script src="src/utils.js"></script>
    <script src="build/js/rockets.js"></script>
    <script src="build/js/birds.js"></script>
    <script src="build/js/lightTower.js"></script>
    <script src="build/js/firework.js"></script> 
    <script src="build/js/meteor.js"></script> 
    <script src="build/js/stars.js"></script> 
    <script src = 'build/js/world.js'></script>










</body>

</html>